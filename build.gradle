buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:5.3.2'
    }
}

plugins {
    // Base
    id 'java'

    // Publishing on bintray
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

apply plugin: 'maven'
apply plugin: 'org.owasp.dependencycheck'

def app_version = '0.17.0'

// Java source & target version
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
    maven { url "https://dl.bintray.com/terl/lazysodium-maven" }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // SaltyRTC
    implementation('org.saltyrtc.client:saltyrtc-client:0.12.+') {
        exclude group: 'org.json'
        exclude module: 'msgpack-core'
        exclude module: 'jackson-dataformat-msgpack'
    }

    // The production code uses the SLF4J logging API at compile time
    implementation 'org.slf4j:slf4j-api:1.7.25'

    // Dependency: Msgpack serialization
    implementation 'org.msgpack:msgpack-core:0.8.16'
    implementation 'org.msgpack:jackson-dataformat-msgpack:0.8.16'

    // Dependency: Binary chunking
    implementation 'org.saltyrtc.chunked-dc:chunked-dc:1.0.+'

    // Test dependencies
    testImplementation 'com.goterl.lazycode:lazysodium-java:3.3.0'
    testImplementation 'org.slf4j:slf4j-simple:1.7.+'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
}

test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// Custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Turn off linting for javadoc
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// Add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

// Publishing
publishing {
    publications {
        SaltyRTC(MavenPublication) {
            from components.java
            groupId 'org.saltyrtc.tasks.webrtc'
            artifactId 'saltyrtc-task-webrtc'
            version app_version
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

// Upload to Bintray
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['SaltyRTC']
    pkg {
        repo = 'maven'
        name = 'saltyrtc-task-webrtc'
        userOrg = 'saltyrtc'
        desc = 'WebRTC Task for SaltyRTC.'
        websiteUrl = 'https://saltyrtc.org/'
        issueTrackerUrl = 'https://github.com/saltyrtc/saltyrtc-task-webrtc-java/issues'
        vcsUrl = 'https://github.com/saltyrtc/saltyrtc-task-webrtc-java.git'
        githubRepo = 'saltyrtc/saltyrtc-task-webrtc-java'
        publicDownloadNumbers = true
        licenses = ['MIT', 'Apache-2.0']
        labels = ['saltyrtc', 'webrtc', 'data channels', 'signaling', 'secure']
        version {
            name = app_version
            desc = 'SaltyRTC WebRTC Task v${app_version}'
            released = new Date()
            vcsTag = 'v${app_version}'
        }
    }
}

// Upload to local Maven repo for testing
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('/tmp/maven/'))
            pom.version = app_version
            pom.groupId = 'org.saltyrtc.tasks.webrtc'
            pom.artifactId = 'saltyrtc-task-webrtc'
        }
    }
}

dependencyCheck {
    skipConfigurations += 'lintClassPath'
    failBuildOnCVSS 0
}
